apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "fm.doe.national"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        buildConfigField "int", "DATA_BASE_VERSION", "1"
        buildConfigField "String", "SURVEYS_FILE_NAME", "\"survey.xml\""
        buildConfigField "String", "SCHOOLS_FILE_NAME", "\"schools.csv\""

        def DROPBOX_API_KEY = "byvebr4n2uyqzj2"
        buildConfigField "String", "DROPBOX_API_KEY", "\"" + DROPBOX_API_KEY + "\""
        manifestPlaceholders = [dropbox_api_key: DROPBOX_API_KEY]

        ext.betaDistributionGroupAliases = "testaccounts"
    }
    signingConfigs {
        release {
            keyAlias System.getenv("KEY_ALIAS") ?: "KEY_ALIAS"
            storePassword System.getenv("KEYSTORE_PASSWORD") ?: "KEYSTORE_PASSWORD"
            storeFile file(System.getenv("KEY_FILE") ?: "KEY_FILE")
            keyPassword System.getenv("KEY_PASSWORD") ?: "KEY_PASSWORD"
        }
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('debug.keystore')
            storePassword 'android'
        }
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '28.0.3'

}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'com.google.android.material:material:1.1.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    // Google Play Services + Drive
    implementation 'com.google.android.gms:play-services-drive:16.0.0'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'

    // WorkManager
    implementation 'android.arch.work:work-runtime:1.0.0-beta01'

    // Moxy
    implementation "com.arello-mobile:moxy-app-compat:1.5.5"
    annotationProcessor "com.arello-mobile:moxy-compiler:1.5.5"

    // RxJava2
    implementation 'io.reactivex.rxjava2:rxjava:2.2.5'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    // Retrofit
    implementation "com.squareup.okhttp3:logging-interceptor:3.12.1"
    implementation "com.squareup.okhttp3:okhttp:3.12.1"
    implementation "com.squareup.retrofit2:retrofit:2.5.0"
    implementation "com.squareup.retrofit2:converter-gson:2.5.0"
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    // Ormlite
    implementation 'com.j256.ormlite:ormlite-core:4.48'
    implementation 'com.j256.ormlite:ormlite-android:4.48'

    // Dagger
    implementation "com.google.dagger:dagger-android:2.16"
    annotationProcessor "com.google.dagger:dagger-android-processor:2.16"
    annotationProcessor "com.google.dagger:dagger-compiler:2.16"

    // OmegaLibs
    implementation 'com.github.Omega-R:OmegaRecyclerView:1.9.0@aar'
    implementation 'com.github.Omega-R:OmegaTypes:0.0.4'
    implementation 'com.github.Omega-R.OmegaIntentBuilder:core:1.1.5'

    // Butterknife
    implementation "com.jakewharton:butterknife:10.0.0"
    annotationProcessor "com.jakewharton:butterknife-compiler:10.0.0"

    implementation ("org.simpleframework:simple-xml:2.7.1") {
        exclude group: 'xpp3', module: 'xpp3'
    }

    // FastCsv
    implementation "de.siegmar:fastcsv:1.0.2"

    // Tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.8@aar') {
        transitive = true
    }

    // CircleImageView
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    // Dropbox
    implementation "com.dropbox.core:dropbox-core-sdk:3.0.11"
}
