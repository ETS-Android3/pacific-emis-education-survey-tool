apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "fm.doe.national"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        buildConfigField "int", "DATA_BASE_VERSION", "1"
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "SURVEYS_FILE_NAME", "\"survey.xml\""
        buildConfigField "String", "SCHOOLS_FILE_NAME", "\"schools.csv\""

        def DROPBOX_API_KEY = "byvebr4n2uyqzj2"
        buildConfigField "String", "DROPBOX_API_KEY", "\"" + DROPBOX_API_KEY + "\""
        manifestPlaceholders = [dropbox_api_key: DROPBOX_API_KEY]

        ext.betaDistributionGroupAliases = "testaccounts"
    }
    signingConfigs {
        release {
            keyAlias System.getenv("KEY_ALIAS") ?: "KEY_ALIAS"
            storePassword System.getenv("KEYSTORE_PASSWORD") ?: "KEYSTORE_PASSWORD"
            storeFile file(System.getenv("KEY_FILE") ?: "KEY_FILE")
            keyPassword System.getenv("KEY_PASSWORD") ?: "KEY_PASSWORD"
        }
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('debug.keystore')
            storePassword 'android'
        }
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '27.0.3'
}

dependencies {
    def supportVersion = '27.1.1'
    def ormliteVersion = '4.48'
    def daggerVersion = '2.11'
    def moxyVersion = '1.5.5'
    def retrofitVersion = '2.3.0'
    def okhttpVersion = '3.4.1'
    def butterknifeVersion = '8.8.1'
    def fastCsvVersion = '1.0.2'
    def gpsDriveVersion = '15.0.1'
    def gpsAuthVersion = '16.0.0'
    def dropboxVersion = '3.0.8'
    def multidexVersion = '1.0.3'
    def constraintVersion = '1.1.3'
    def workManagerVersion = '1.0.0-alpha08'
    def simpleXmlVersion = '2.7.1'

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"

    // WorkManager
    implementation "android.arch.work:work-runtime:$workManagerVersion"

    // moxy
    implementation "com.arello-mobile:moxy-app-compat:$moxyVersion"
    annotationProcessor "com.arello-mobile:moxy-compiler:$moxyVersion"

    // RxJava2
    implementation 'io.reactivex.rxjava2:rxjava:2.1.16'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    // RxBinding
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'

    // Retrofit
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    // Ormlite
    implementation "com.j256.ormlite:ormlite-core:$ormliteVersion"
    implementation "com.j256.ormlite:ormlite-android:$ormliteVersion"

    // Dagger
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    // OmegaLibs
    implementation 'com.github.Omega-R:OmegaRecyclerView:1.7.6@aar'
    implementation 'com.github.Omega-R:OmegaLayouts:0.0.1@aar'
    implementation 'com.github.Omega-R:OmegaTypes:0.0.3'
    implementation 'com.github.Omega-R.OmegaIntentBuilder:core:1.1.2'

    // Butterknife
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    implementation ("org.simpleframework:simple-xml:$simpleXmlVersion") {
        exclude group: 'xpp3', module: 'xpp3'
    }

    // FastCsv
    implementation "de.siegmar:fastcsv:$fastCsvVersion"

    // Tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }

    // CircleImageView
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    // Google Play Services + Drive
    implementation "com.google.android.gms:play-services-drive:$gpsDriveVersion"
    implementation "com.google.android.gms:play-services-auth:$gpsAuthVersion"

    // Dropbox
    implementation "com.dropbox.core:dropbox-core-sdk:$dropboxVersion"

    implementation "com.android.support:multidex:$multidexVersion"
}
