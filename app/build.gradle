apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "fm.doe.national"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        buildConfigField "int", "DATA_BASE_VERSION", "1"
        buildConfigField "String", "SURVEYS_FILE_NAME", "\"survey.xml\""
        buildConfigField "String", "SCHOOLS_FILE_NAME", "\"schools.csv\""
        buildConfigField "float", "SCHOOL_EVALUATION_LEVEL_MULTIPLIER", "90.0f"
        buildConfigField "float", "CLASSROOM_OBSERVATION_LEVEL_MULTIPLIER", "10.0f"

        def DROPBOX_API_KEY = "byvebr4n2uyqzj2"
        buildConfigField "String", "DROPBOX_API_KEY", "\"" + DROPBOX_API_KEY + "\""
        manifestPlaceholders = [dropbox_api_key: DROPBOX_API_KEY]

        ext.betaDistributionGroupAliases = "testaccounts"
    }
    signingConfigs {
        release {
            keyAlias System.getenv("KEY_ALIAS") ?: "KEY_ALIAS"
            storePassword System.getenv("KEYSTORE_PASSWORD") ?: "KEYSTORE_PASSWORD"
            storeFile file(System.getenv("KEY_FILE") ?: "KEY_FILE")
            keyPassword System.getenv("KEY_PASSWORD") ?: "KEY_PASSWORD"
        }
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('debug.keystore')
            storePassword 'android'
        }
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '28.0.3'

}

dependencies {
    def roomVersion = '2.1.0-beta01'
    def moxyVersion = '1.5.7'
    def okhttpVersion = '4.0.0-alpha01'
    def retrofitVersion = '2.5.0'
    def daggerVersion = '2.22.1'
    def butterknifeVersion = '10.1.0'
    def workVersion = '2.0.1'

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.appcompat:appcompat:1.1.0-alpha05'
    implementation 'com.google.android.material:material:1.1.0-alpha06'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    // Google Play Services + Drive
    implementation 'com.google.android.gms:play-services-drive:16.1.0'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'

    // WorkManager
    implementation "androidx.work:work-runtime:$workVersion"
    implementation "androidx.work:work-rxjava2:$workVersion"

    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"

    // Moxy
    implementation "com.github.Omega-R.OmegaMoxy:moxy-androidx:$moxyVersion"
    annotationProcessor "com.github.Omega-R.OmegaMoxy:moxy-compiler:$moxyVersion"

    // RxJava2
    implementation 'io.reactivex.rxjava2:rxjava:2.2.5'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    // Retrofit
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    // Dagger
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    // OmegaLibs
    implementation 'com.github.Omega-R:OmegaRecyclerView:1.9.6@aar'
    implementation 'com.github.Omega-R:OmegaTypes:0.2.0'
    implementation 'com.github.Omega-R.OmegaIntentBuilder:core:1.1.6'
    implementation 'com.github.Omega-R:OmegaViews:1.0.0'

    // Butterknife
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    implementation("org.simpleframework:simple-xml:2.7.1") {
        exclude group: 'xpp3', module: 'xpp3'
    }

    // FastCsv
    implementation 'de.siegmar:fastcsv:1.0.3'

    // Tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'

    implementation('com.crashlytics.sdk.android:crashlytics:2.10.0@aar') {
        transitive = true
    }

    // CircleImageView
    implementation 'de.hdodenhof:circleimageview:3.0.0'

    // Dropbox
    implementation "com.dropbox.core:dropbox-core-sdk:3.0.11"
}
